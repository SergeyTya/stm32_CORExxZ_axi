//*****************************************************************************
//
// IQcos.S - Fixed point calculation of cos in radians
//
// Copyright (c) 2010 Texas Instruments Incorporated.  All rights reserved.
// TI Information - Selective Disclosure
//
//*****************************************************************************

//*****************************************************************************
//
// Function:   _IQNcos
//
//*****************************************************************************
//
// C Usage:    extern long _IQNcos(long U);
//
//-----------------------------------------------------------------------------
//
// On Entry:   r0    = U in IQ format
//
//
// Regs Used:  r0-r3, r12
//
// On Exit:    r0    = cos(U)
//
//
// Q range:    29 to 1
//      NOTE:   This will actually generate 30 to 1, but 30 is not officially
//              "on the list". C2000's documentation states that it is only 
//              1-29, but has header entries for 30 as well.  In any case, Q30
//              generates up to 4 counts of error, but is otherwise usable.
//
//-----------------------------------------------------------------------------
//
// Algorithm:  The procedure for calculating "Y = cos(U)" is as follows:
//
//      Step 1)  Normalize U to be within Quadrant I
//
//      Step 2)  Use upper 9 bits to lookup sin(index) and cos(index).
//
//      Step 3)  Use Taylor series to estimate cos(index+remainder).
//               Up to 6 terms used, dependendent on Q
//
//      Note:   Index == 0 is handled as a special case.  It is a linear 
//              approximation with a slope just less than pi.  Value was 
//              chosen to minimize maximal error : +/-2 counts in Q30.
//
//##### INTERNAL BEGIN #####
//-----------------------------------------------------------------------------
// Future Work:
//
//   It may be possible to fold this function in with the PU implementation. 
//   The differences are only at the head of the function, and are listed 
//   below:
//      Load and multiply by 1/pi
//      The left shift to truncate is 64 bits instead of 32-bits.
//   Making the 64-bit left shift "fallable" may be difficult. Best guess would
//   be to change the macro definition to _IQsin(_iq x,0);.  This would allow
//   us to always use the add instruction without fear of what might be in r1.
//
//   As currently implemented, the residual is initialized to zero.  
//   A quick improvement would be to skip the initialization and fold it into 
//   the following add or rsb instruction.  This would be dependendant on Q. 
//   A better option may be to initialize it to 1/(2*n!)*{sin,cos}(index).
//   This might allow us to fake out an entire order of the expansion as a 
//   pseudo constant, and save us a few multiplies.  Optimal value not yet 
//   calculated, it would have to be done similarly to the index == 0 
//   optimization.
//
//   Consider returning to a full-wave implementation (instead of quarter-wave)
//   This would require signed math (2-3 cycle penalty per multiply, for SMULL
//   vs UMULL, plus additional shifting/ rounding), but would eliminate 
//   the expensive setup / tear down of the function.  Would also eliminate
//   special case handling.  If this path is chosen, center-align the Taylor-
//   series.
//
//   This implementation is "tuneable", in that it is relatively easy to 
//   choose how many terms in the expansion are active for each Q.  They are
//   currently chosen such that we hit +/-1 count of accuracy for all Q: it is
//   expensive to do better due to fixed point rounding. If desired, these 
//   choices can be revisited later with speed in mind.
//
//##### INTERNAL END #####
//-----------------------------------------------------------------------------
//
// Benchmark:
//
//        Q <= 30       : 78 Cycles
//        Q <= 29       : 71 Cycles
//   9 <= Q <= 20       : 59 Cycles
//   1 <= Q <=  8       : 31 Cycles
//
//  These are all worst case: Quadrant 3.  Others are 1-4 quicker.
//
//*****************************************************************************
__PREAMBLE__
//*****************************************************************************
//
// The sin/cos table is used.
//
//*****************************************************************************
    __IMPORT__ sincos_table

//$ BEGIN_UNROLL _QQ_ 1 to 8
//*****************************************************************************
//
// _IQ_QQ_cos
//
//*****************************************************************************
    __SECTION__ _IQ_QQ_cos
    __EXPORT__ _IQ_QQ_cos__THUMB_LABEL__
_IQ_QQ_cos__LABEL__
    //
    // Convert from radians to radians per unit by dividing by pi
    // This is done by multiplying by 1/pi in Q32
    //
    ldr     r1, invpiq32__QQ_
    smull   r1, r0, r1, r0

    //
    // Load the table pointer into memory early, since we have more than
    // enough registers to handle it.
    //
    ldr     r3, sincos_table__QQ_

    //
    // Put the input into Q31.
    // Since this is in Per Unit radians, we only care about the 1s digit
    // to determine sign of the result.  Higher bits are useless.
    //
    lsl     r0, r0, #(31 - _QQ_)
    add     r0, r0, r1, lsr #(_QQ_ + 1)
    add     r0, r0, #0x40000000

    //
    // If the fractional portion is greater than 0.5, subtract it from 1.0
    // This is because sin(x) = sin(1-x) in per unit radians, and we only store
    // the [0,.5) in the table.
    //
    lsls    r1, r0, #2
    it      cs
        rsbcs   r0, r0, #0x80000000

    //
    // Remove the sign bit and store it in the carry flag
    //
    lsls    r0, r0, #1

    //
    // Lookup the value from the table.
    //
    ubfx    r1, r0, #(32 - 10), #10
    ldr     r0, [r3, r1, lsl #2]

    //
    // Sign compensate, shift down for Q and round.
    //
    lsr     r0, r0, #1
    it      cs
        rsbcs   r0, r0, #0
    asrs    r0, r0, #(31 - _QQ_)
    adc     r0, r0, #0

    //
    // Return to the caller.
    //
    bx      lr

    //
    // The literal pool.
    //
    __LITERALS__
sincos_table__QQ___LABEL__
    __WORD__ sincos_table
invpiq32__QQ___LABEL__
    __WORD__ 0x517cc1b7         // 1/pi in Q32

//$ END_UNROLL _QQ_
//$ BEGIN_UNROLL _QQ_ 9 to 29
//*****************************************************************************
//
// _IQ_QQ_cos
//
//*****************************************************************************
    __SECTION__ _IQ_QQ_cos
    __EXPORT__ _IQ_QQ_cos__THUMB_LABEL__
_IQ_QQ_cos__LABEL__
    //
    // Convert from radians to radians per unit by dividing by pi
    // This is done by multiplying by 1/pi in Q32
    //
    ldr     r1, invpiq32__QQ_
    smull   r1, r0, r1, r0

    //
    // Put the input into Q31.
    // Since this is in Per Unit radians, we only care about the 1s digit
    // to determine sign of the result.  Higher bits are useless.
    // This is equivalent to sin(x) = sin(x+2*n) in per unit radians.
    //
    lsl     r0, r0, #(31 - _QQ_)
    add     r0, r0, r1, lsr #(_QQ_ + 1)
    add     r0, r0, #0x40000000

    //
    // If the fractional portion is greater than 0.5, subtract it from 1.0
    // This is equivalent to sin(x) = sin(1-x) in per unit radians.
    // r0 becomes UQ32 representation of input constrained to 1st Quadrant PU:
    // [0,.5)
    //
    lsls    r1, r0, #2
    it      cs
        rsbcs   r0, r0, #0x80000000
    lsls    r0, r0, #1

    //
    // Use the top 9 fractional bits as an index into the look up table.
    // Pull sine and cosine of the index.
    // Jump out if we're close enough to 0 to do a fully odd approximation.
    //
    ldr     r12, sincos_table__QQ_
    ubfx    r1, r0, #(31 - 9), #10
    cbz     r1, cosindex0__QQ_
    ldr     r2, [r12, r1, lsl #2]
    rsb     r1, r1, #512
    ldr     r3, [r12, r1, lsl #2]

    //
    // Put delta x in r1.
    // This is (x-index)*pi/2  in Q32
    // Consider replacing lsl/lsr combo with UBFX:
    // 1 cycle faster, but slightly less accurate delta x.
    //
    lsl     r1, r0, #(9 + 1)
    ldr     r12, piq30__QQ_
    umull   r12, r1, r1, r12
    lsr     r1, r1, #(9 - 1)
    mov     r0, #0

    //
    // Calculates the taylor series expansion of sin(x)
    // Interval is 0:90 degrees.
    // Preconditions:
    // r0 = 0x00000000
    // r1 = remainder (x-index) in Q32 Radians
    // r2 = sin(index) in Q32
    // r3 = cos(index) in Q32
    // r12 is available as scratch space
    // ASPR contains sign flag as MI=flip sign.
#if _QQ_ > 30
    //
    // Calculate 7th term of taylor series expansion.
    // This performs y = remainder/7*(cos(index)-y)
    // 8-12 cycles
    //
    sub     r0, r3, r0
    umull   r12, r0, r0, r1
    ldr     r12, max7_QQ_       // 2**32/7
    umull   r12, r0, r12, r0

#endif
#if _QQ_ > 28
    //
    // Calculate 6th term of taylor series expansion.
    // This performs y = remainder/6*(sin(index)+y)
    // 5-7 cycles
    //
    add     r0, r2, r0
    umull   r12, r0, r0, r1
    ldr     r12, max6_QQ_       // 2**32/6
    umull   r12, r0, r0, r12
    
#endif
#if _QQ_ > 20
    //
    // Calculate 5th term of taylor series expansion.
    // This performs y = remainder/5*(cos(index)-y)
    // 8-12 cycles
    //
    sub     r0, r3, r0
    umull   r12, r0, r0, r1
    mov     r12, #0x33333333    // 2**32/5
    umull   r12, r0, r12, r0

#endif
#if _QQ_ > 20
    //
    // Calculate 4th term of taylor series expansion.
    // This performs y = remainder/4*(sin(index)+y)
    // 5-7 cycles
    //
    add     r0, r2, r0
    umull   r12, r0, r0, r1
    lsr     r0, r0, #2

#endif
#if _QQ_ > 20
    //
    // Calculate 3rd term of taylor series expansion.
    // This performs y = remainder/3*(cos(index)-y)
    // 9-13 cycles
    //
    sub     r0, r3, r0
    umull   r12, r0, r0, r1
    mov     r12, #0x55555555    // 2**32/3
    umull   r12, r0, r12, r0

#endif
    //
    // Calculate 2nd term of taylor series expansion.
    // This performs y = remainder/2*(sin(index)+y)
    // 5-7 cycles
    //
    add     r0, r2, r0
    umull   r12, r0, r0, r1
    lsr     r0, r0, #1

    //
    // Calculate 1st term of taylor series expansion.
    // This performs y = remainder/1*(cos(index)-y)
    // 4-6 cycles
    //
    sub     r0, r3, r0
    umull   r12, r0, r0, r1

    //
    // Calculate 0th term of taylor series expansion.
    // This performs y = (sin(index)+y)
    //
    add     r0, r2, r0

    //
    // Compensate for sign flag in r5
    //
    lsr     r0, r0, #1
    it      cs
        rsbcs   r0, r0, #0
    asrs    r0, r0, #(31 - _QQ_)
    adc     r0, r0, #0

    //
    // Return to the caller.
    //
    bx      lr

    //
    // This branch handles the case very close to 0.
    // It uses the sin(x)=x approximation, but the constant is fudged down
    // a bit from pi to minimize the maximum error to +/-2 counts at Q30
    //
cosindex0__QQ___LABEL__
    ldr     r12, piq30_compensated__QQ_
    umull   r12, r0, r0, r12
#if _QQ_ < 30
    lsr     r0, r0, #(30 - _QQ_)
#endif
    it      cs
        rsbcs   r0, r0, #0
    bx      lr

    //
    // The literal pool.
    //
    __LITERALS__
sincos_table__QQ___LABEL__
    __WORD__ sincos_table
piq30__QQ___LABEL__
    __WORD__ 0xc90fdaa2         // pi in Q30
piq30_compensated__QQ___LABEL__
    __WORD__ 0xc90fcd37         // slightly less than pi in Q30
invpiq32__QQ___LABEL__
    __WORD__ 0x517cc1b7         // 1/pi in Q32
#if _QQ_ > 30
max7_QQ___LABEL__
    __WORD__ 0x24924924         // 2**32/7
#endif
#if _QQ_ > 28
max6_QQ___LABEL__
    __WORD__ 0x2aaaaaaa         // 2**32/6
#endif

//$ END_UNROLL _QQ_
    __END__
